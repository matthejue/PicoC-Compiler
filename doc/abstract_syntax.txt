===============================================================================
=                                   L_PicoC                                   =
===============================================================================
----------------------------------- L_Arith -----------------------------------
un_op ::= Minus() | Not()
bin_op ::= Add() | Sub() | Mul() | Div() | Mod() | Oplus() | And() | Or()
exp ::= Name(str) | Num(str) | Char(str)
      | BinOp(<exp>, <bin_op>, <exp>)
      | UnOp(<un_op>, <exp>) | Call(Name('input'), None)
stmt ::= Exp(Call(Name('print'), <exp>))
----------------------------------- L_Logic -----------------------------------
un_op ::= LogicNot()
relation ::= Eq() | NEq() | Lt() | LtE() | Gt() | GtE()
bin_op ::= LogicAnd() | LogicOr()
exp ::= Atom(<exp>, <relation>, <exp>)
      | ToBool(<exp>)
-------------------------------- L_Assign_Alloc -------------------------------
type_qual ::= Const() | Writeable()
size_qual ::= IntType() | CharType() | VoidType() | <struct_spec>
assign_lhs ::= Alloc(<type_qual>, <size_qual>, <pntr_decl>) | <ref_loc>
exp_stmts ::= Alloc(<type_qual>, <size_qual>, <pntr_decl>)
stmt ::= Exp(<exp_stmts>)
       | Assign(<assign_lhs>, <exp>)
---------------------------------- L_Pointer ----------------------------------
pntr_decl ::= PntrDecl(Num(str), <array_decl>)
deref_loc ::= Ref(<ref_loc>)|<ref_loc>
ref_loc ::= Name(str)
          | Deref(<deref_loc>, <exp>)
          | Subscr(<deref_loc>, <exp>)
          | Attr(<ref_loc>, Name(str))
exp ::= Deref(<deref_loc>, <exp>)
      | Ref(<ref_loc>)
----------------------------------- L_Array -----------------------------------
array_decl ::= ArrayDecl(Name(str), Num(str)*) | ArrayDecl(<pntr_decl>, Num(str)*)
exp ::= Subscr(<deref_loc>, <exp>)
stmt ::= Assign(Alloc(Writeable(), <size_qual>, PntrDecl(Num("0"), ArrayDecl(Name(str), Num(str)+))), Array(<exp>+))
----------------------------------- L_Struct ----------------------------------
struct_spec ::= StructSpec(Name(str))
exp ::= Attr(<ref_loc>, Name(str))
stmt ::= Assign(Alloc(Writeable(), StructSpec(Name(str)), PntrDecl(Num("0"), ArrayDecl(Name(str), []))), Struct(Assign(Name(str), <exp>)+))
decl_def ::= StructDecl(Name(str), Param(<size_qual>, Name(str))+)
---------------------------------- L_If_Else ----------------------------------
stmt ::= If(<exp>, <stmt>*)
       | IfElse(<exp>, <stmt>*, <stmt>*)
------------------------------------ L_Loop -----------------------------------
stmt ::= While(<exp>, <stmt>*)
       | DoWhile(<exp>, <stmt>*)
------------------------------------ L_Fun ------------------------------------
fun_decl ::= FunType(<size_qual>)
exp ::= Call(Name(str), <exp>*) | Null()
exp_stmts ::= Call(Name(str), <exp>*)
stmt ::= Return(<exp>)
decl_def ::= FunDecl(<size_qual>, Name(str), Param(<size_qual>, Name(str))*)
           | FunDef(<size_qual>, Name(str), Param(<size_qual>, Name(str))*, <stmt>*)
------------------------------------ L_File -----------------------------------
file ::= File(Name(str), <decl_def>*)
===============================================================================
=                                L_PicoC_Blocks                               =
===============================================================================
----------------------------------- L_Blocks ----------------------------------
stmt ::= Goto(Name(str))
block ::= Block(Name(str), <stmt>*)
decl_def ::= FunDef(<size_qual>, Name(str), Param(<size_qual>, Name(str))*, <block>*)
===============================================================================
=                                L_RETI_Blocks                                =
===============================================================================
----------------------------------- L_Blocks ----------------------------------
program ::= Program(Name(str), <block>*)
block ::= Block(Name(str), <instr>*)
goto ::= Goto(Name(str))
===============================================================================
=                                    L_RETI                                   =
===============================================================================
---------------------------------- L_Program ----------------------------------
reg ::= Reg('ACC') | Reg('IN1') | Reg('IN2') | Reg('PC') | Reg('SP')
      | Reg('BAF') | Reg('CS') | Reg('DS')
arg ::= <reg> | Num(val)
rel ::= Eq() | NEq() | Lt() | LtE() | Gt() | GtE() | Always() | NOp()
op ::= Add() | Addi() | Sub() | Subi() | Mult() | Multi() | Div() | Divi()
      | Mod() | Modi() | Oplus() | Oplusi() | Or() | Ori() | And() | Andi()
op ::= Load() | Loadin() | Loadi()
op ::= Store() | Storein() | Move()
instr ::= Instr(<op>, <arg>+) | Jump(<rel>, Num(val)) | Int(Num(val)) |, RTI()
        | Call(Name('print'), <reg>) | Call(Name('input'), <reg>)
program ::= Program(Name(str), <instr>*)
