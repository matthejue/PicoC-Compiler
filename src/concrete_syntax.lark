// ----------------------------------------------------------------------------
// -                                   Lexer                                  -
// ----------------------------------------------------------------------------
// --------------------------------- L_Comment --------------------------------
COMMENT: "//" /[^\n]*/|"/*" /(.|\n)*?/ "*/"
// ---------------------------------- L_Arith ---------------------------------
DIG_NO_0: "1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9"
DIG_WITH_0: "0"|DIG_NO_0
NUM: "0"|DIG_NO_0 DIG_WITH_0*
LETTER: "a".."z"|"A".."Z"
CHAR: "'"LETTER"'"
NAME: (LETTER|"_")(LETTER|DIG_WITH_0|"_")*
UN_OP: "-"|"~"|"!"
PREC1_OP: "*"|"/"|"%"
PREC2_OP: "+"|"-"|"^"|"&"|"|"
// ---------------------------------- L_Logic ---------------------------------
// ! is new but gets added under L_Arith
RELATION: "=="|"!="|"<"|"<="|">"|">="
// ------------------------------ L_Assign_Alloc ------------------------------
PRIM_SIZE_QUAL: "int"|"char"|"void"
// ----------------------------------------------------------------------------
// -                                  Parser                                  -
// ----------------------------------------------------------------------------
// ---------------------------------- L_Arith ---------------------------------
input_odp: "input""("")"
?arith_opd: NAME|NUM|CHAR|input_odp|"("logic_exp")"|logic_opd|pntr_opd|array_opd|struct_opd|fun_call
un_opd: UN_OP un_opd|arith_opd
arith_prec1: un_opd (PREC1_OP un_opd)*
arith_prec2: arith_prec1 (PREC2_OP arith_prec1)*
?arith_exp: arith_prec2
print_stmt: "print""("arith_exp")"";"
// ---------------------------------- L_Logic ---------------------------------
to_bool: arith_exp
?logic_opd: "("logic_exp")"|to_bool
logic_atom: logic_opd [RELATION logic_opd]
logic_and: logic_atom ("&&" logic_atom)*
logic_or: logic_and ("||" logic_and)*
?logic_exp: logic_or
?arith_exp_logic_exp: arith_exp|logic_exp
// ------------------------------ L_Assign_Alloc ------------------------------
?size_qual: PRIM_SIZE_QUAL|pntr_size_qual|array_size_qual|struct_size_qual|fun_size_qual
alloc: size_qual NAME";"
var_assign: NAME "=" arith_exp_logic_exp";"
?assign: var_assign|pntr_assign|array_assign|struct_assign
init: size_qual NAME "=" arith_exp_logic_exp";"
const_init: "const" size_qual NAME "=" NUM";"
?assign_alloc_stmt: alloc|assign|init|const_init
// --------------------------------- L_Pointer --------------------------------
pntr_size_qual: size_qual"*"
pntr_simple: arith_opd
!pntr_arith: "("arith_opd"+"arith_exp_logic_exp")"|"("arith_opd"-"arith_exp_logic_exp")"
?deref: "*"(pntr_simple|pntr_arith)
var_ref: "&"NAME
pntr_ref: "&"deref
?ref: var_ref|pntr_ref|array_ref|struct_ref
?pntr_opd: deref|ref
pntr_assign: deref "=" arith_exp_logic_exp";"
// ---------------------------------- L_Array ---------------------------------
array_size_qual: size_qual"["[NUM]"]"
array_creation: "{"arith_exp_logic_exp("," arith_exp_logic_exp)*"}"
subscript: arith_opd"["arith_exp_logic_exp"]"
?array_opd: array_creation|subscript
array_ref: "&"subscript
array_assign: subscript "=" arith_exp_logic_exp";"
// --------------------------------- L_Struct ---------------------------------
struct_size_qual: "struct" NAME
struct_creation: "{""."NAME"="arith_exp_logic_exp("," "."NAME"="arith_exp_logic_exp)*"}"
attr: NAME"."NAME
?struct_opd: struct_creation|attr
struct_ref: "&"attr
struct_assign: attr "=" arith_exp_logic_exp";"
struct_decl_stmt: "struct" NAME "{"size_qual NAME(";" size_qual NAME)*"}"";"
// --------------------------------- L_If_Else --------------------------------
if_: "if""("arith_exp_logic_exp")" "{" stmt* "}"
if_else: "if""("arith_exp_logic_exp")" "{" stmt* "}" "else" "{" stmt* "}"|"if""("arith_exp_logic_exp")" "{" stmt* "}" "else" stmt
?if_else_stmt: if_|if_else
// ---------------------------------- L_Loop ----------------------------------
while_: "while""("arith_exp_logic_exp")" "{" stmt* "}"
do_while: "do" "{" stmt* "}" "while""("arith_exp_logic_exp")"";"
?loop_stmt: while_|do_while
// ---------------------------------- L_Stmt ----------------------------------
?stmt: print_stmt|assign_alloc_stmt|struct_decl_stmt|if_else_stmt|loop_stmt|fun_stmt
// ----------------------------------- L_Fun ----------------------------------
fun_size_qual: "fun" size_qual
fun_call: NAME"("[arith_exp_logic_exp("," arith_exp_logic_exp)*]")"
return_: "return" arith_exp_logic_exp
?fun_stmt: fun_call|return_
def_: size_qual NAME"("[size_qual NAME("," size_qual NAME)*]")" "{" stmt* "}"
// ---------------------------------- L_File ----------------------------------
file: NAME def_*

%import common.WS
%ignore WS
%ignore COMMENT
