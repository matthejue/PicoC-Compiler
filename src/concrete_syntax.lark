// ----------------------------------------------------------------------------
// -                                   Lexer                                  -
// ----------------------------------------------------------------------------
// --------------------------------- L_Comment --------------------------------
COMMENT: "//" /[^\n]*/|"/*" /(.|\n)*?/ "*/"
// ---------------------------------- L_Arith ---------------------------------
DIG_NO_0: "1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9"
DIG_WITH_0: "0"|DIG_NO_0
NUM: "0"|DIG_NO_0 DIG_WITH_0*
LETTER: "a".."z"|"A".."Z"
CHAR: "'"LETTER"'"
NAME: (LETTER|"_")(LETTER|DIG_WITH_0|"_")*
UN_OP: "-"|"~"|"!"
PREC1_OP: "*"|"/"|"%"
PREC2_OP: "+"|"-"|"^"|"&"|"|"
// ---------------------------------- L_Logic ---------------------------------
// ! is new but gets added under L_Arith
RELATION: "=="|"!="|"<"|"<="|">"|">="
// ------------------------------ L_Assign_Alloc ------------------------------
SIZE_QUAL: "int"|"char"|"void"
// --------------------------------- L_Pointer --------------------------------
PNTR_PLUS: "+"
PNTR_MINUS: "-"
// --------------------------------- L_If_Else --------------------------------
ELSE: "else"
// ----------------------------------------------------------------------------
// -                                  Parser                                  -
// ----------------------------------------------------------------------------
// ---------------------------------- L_Arith ---------------------------------
input_odp: "input""("")"
?arith_opd: NAME|NUM|CHAR|input_odp|"("logic_exp")"|pntr_opd|array_subscr|struct_attr|fun_call
un_opd: UN_OP* arith_opd
arith_prec1: un_opd (PREC1_OP un_opd)*
arith_prec2: arith_prec1 (PREC2_OP arith_prec1)*
?arith_exp: arith_prec2
print_stmt: "print""("arith_exp")"";"
// ---------------------------------- L_Logic ---------------------------------
to_bool: arith_exp
?logic_opd: "("logic_exp")"|to_bool
logic_atom: logic_opd [RELATION logic_opd]
logic_and: logic_atom ("&&" logic_atom)*
logic_or: logic_and ("||" logic_and)*
?logic_exp: logic_or
?arith_exp_logic_exp: arith_exp|logic_exp
// ------------------------------ L_Assign_Alloc ------------------------------
?datatype: SIZE_QUAL|struct_spec
alloc: datatype pntr_decl NAME array_decl
?alloc_stmt: alloc";"
var_assign: NAME "=" arith_exp_logic_exp";"
?assign: var_assign|pntr_assign|array_assign|struct_assign
init: alloc "=" arith_exp_logic_exp";"
const_init: "const" SIZE_QUAL NAME "=" NUM";"
?assign_alloc_stmt: alloc_stmt|assign|init|const_init|array_init|struct_init
// --------------------------------- L_Pointer --------------------------------
!pntr_decl: "*"*
pntr_simple: arith_opd
pntr_arith: "("arith_opd"PNTR_PLUS"arith_exp_logic_exp")"|"("arith_opd"PNTR_MINUS"arith_exp_logic_exp")"
?deref: "*"(pntr_simple|pntr_arith)
var_ref: "&"NAME
pntr_ref: "&"deref
?ref: var_ref|pntr_ref|array_ref|struct_ref
?pntr_opd: deref|ref
pntr_assign: deref "=" arith_exp_logic_exp";"
// ---------------------------------- L_Array ---------------------------------
array_decl: ("["NUM"]")*
array_subscr: arith_opd"["arith_exp_logic_exp"]"
array_ref: "&"array_subscr
array_assign: array_subscr "=" arith_exp_logic_exp";"
?entry_subexp: arith_exp_logic_exp|array_subexps|struct_subexps
?array_subexp: "entry_subexp("," entry_subexp)*"
array_subexps: "{array_subexp}"
array_init: datatype NAME array_decl "=" array_subexps
// --------------------------------- L_Struct ---------------------------------
struct_spec: "struct" NAME
struct_attr: NAME"."NAME
struct_ref: "&"struct_attr
struct_assign: struct_attr "=" arith_exp_logic_exp";"
struct_decl_stmt: "struct" NAME "{"(datatype NAME";")+"}"";"
?struct_subexp: "."NAME"="entry_subexp ("," "."NAME"="entry_subexp)*
struct_subexps: "{"struct_subexp"}"
struct_init: struct_spec NAME "=" struct_subexps
// --------------------------------- L_If_Else --------------------------------
if_: "if""("arith_exp_logic_exp")" "{" stmts "}"
if_else: "if""("arith_exp_logic_exp")" "{" stmts "}" ELSE "{" stmts "}"|"if""("arith_exp_logic_exp")" "{" stmts "}" ELSE stmt
?if_else_stmt: if_|if_else
// ---------------------------------- L_Loop ----------------------------------
while_: "while""("arith_exp_logic_exp")" "{" stmts "}"
do_while: "do" "{" stmts "}" "while""("arith_exp_logic_exp")"[";"]
?loop_stmt: while_|do_while
// ---------------------------------- L_Stmt ----------------------------------
?decl_part: assign_alloc_stmt
?exec_part: print_stmt|struct_decl_stmt|if_else_stmt|loop_stmt|fun_stmt
?stmts: decl_part* exec_part*
// ----------------------------------- L_Fun ----------------------------------
fun_call_args: [arith_exp_logic_exp("," arith_exp_logic_exp)*]
fun_call: NAME"("fun_call_args")"
fun_call_stmt: fun_call
fun_return: "return" arith_exp_logic_exp
?fun_stmt: fun_call_stmt|fun_return
fun_params: [datatype NAME("," datatype NAME)*]
fun_def: datatype NAME"("fun_params")" "{" stmts "}"
?fun_defs: fun_def*
// ---------------------------------- L_File ----------------------------------
file: NAME fun_defs
// ---------------------------------- L_Block ---------------------------------
// fun_def: datatype NAME"("[datatype NAME("," datatype NAME)*]")" "{" blocks "}"
block: NAME":" stmts
?blocks: block*
goto: "goto""("NAME")"


%import common.WS
%ignore WS
%ignore COMMENT
